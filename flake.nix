# TODO: reintroduce the dbg macro somehow
{
  description = "PANNA: Playground for Approximate Nearest Neighbor Algorithms";

  inputs.nixpkgs.url = "github:NixOS/nixpkgs/nixos-unstable";
  inputs.hl = {
    url = "github:pamburus/hl";
    inputs.nixpkgs.follows = "nixpkgs";
  };
  inputs.flake-utils = {
    url = "github:numtide/flake-utils";
  };
  inputs.sigmod-hdbscan = {
    url = "github:FrancescoMonaco/hdbscan";
    inputs.nixpkgs.follows = "nixpkgs";
  };

  outputs = {
    self,
    nixpkgs,
    hl,
    flake-utils,
    sigmod-hdbscan,
  }:
    flake-utils.lib.eachDefaultSystem (
      system: let
        pkgs = import nixpkgs {inherit system;};
        hl-bin = hl.packages.${system}.default;
        # get a python build with optimizations enabled, following
        # this suggestion: https://discourse.nixos.org/t/why-is-the-nix-compiled-python-slower/18717/9
        python = pkgs.python312;
        # .override {
        #   enableOptimizations = true;
        #   reproducibleBuild = false;
        #   self = python;
        # };

        fast-hdbscan = python.pkgs.buildPythonPackage rec {
          pname = "fast_hdbscan";
          version = "0.2.2";
          src = python.pkgs.fetchPypi {
            inherit pname version;
            hash = "sha256-6/2iCMhdM4FBzGyKa5XJ8vHqg9O5mC+YZLJdc7B9nMg=";
          };
          # do not run tests
          doCheck = false;
          # specific to buildPythonPackage, see its reference
          pyproject = true;
          dependencies = with python.pkgs; [
            numba
            numpy
            scikit-learn
          ];
          build-system = with python.pkgs; [
            setuptools
            wheel
          ];
        };

        mlpack = pkgs.stdenv.mkDerivation rec {
          name = "mlpack";
          version = "4.6.2";
          src = pkgs.fetchurl {
            url = "https://www.mlpack.org/files/mlpack-${version}.tar.gz";
            hash = "sha256-L+dy2jg6k1ZFztB6B7UZQsoXjTgSnfO/aFiQvDwXUs8=";
          };
          installPhase = ''
            mkdir -p $out/include
            cp -r src/* $out/include
          '';
        };

        ensmallen = pkgs.stdenv.mkDerivation rec {
          name = "ensmallen";
          version = "3.10.0";
          src = pkgs.fetchurl {
            url = "https://ensmallen.org/files/ensmallen-${version}.tar.gz";
            hash = "sha256-JI4gNoVveqj6s0ygL6Onmyya8g9TsdJuPek50VDcuzo=";
          };
          installPhase = ''
            mkdir -p $out/include
            cp -r include/* $out/include
          '';
        };

        tree-similarity = pkgs.stdenv.mkDerivation rec {
          pname = "tree-similarity";
          version = "0.1.1";

          src = pkgs.fetchFromGitHub {
            owner = "DatabaseGroup";
            repo = "tree-similarity";
            rev = "0.1.1";
            hash = "sha256-bICwYyxXbZnMTfvkJvlrvm3NN4L+aRSFIl+kII5vSro=";
          };

          nativeBuildInputs = [
            pkgs.cmake
            pkgs.ninja
            pkgs.pkg-config
          ];

          buildInputs = [
            pkgs.llvmPackages.libcxx
          ];

          cmakeFlags = [
            "-DCMAKE_BUILD_TYPE=Release"
            "-GNinja"
          ];
        };

        panna-python = python.pkgs.buildPythonPackage {
          pname = "panna";
          version = "0.0.1";
          pyproject = true;
          src = ./.;
          GIT_COMMIT_HASH = self.rev or "dirty";

          # as stated here, one should disable the cmake setup:
          # https://discourse.nixos.org/t/building-python-package-with-scikit-build-core-and-cmake-dependencies-die-python/69665/2
          dontUseCmakeConfigure = true;
          dontUseCmakeBuild = true;
          dontUseCmakeInstall = true;

          build-system = with python.pkgs; [
            scikit-build-core
            nanobind
          ];
          dependencies = with python.pkgs; [
            numpy
            h5py
          ];
          buildInputs = with pkgs; [
            python.pkgs.build
            catch2_3
            cereal
            hdf5
            highfive
          ];
          nativeBuildInputs = with pkgs; [
            cmake
            git
            ninja
          ];
        };

        # the Python interpreter with all the packages we need
        python-interpreter =
          python.withPackages
          (ppkgs:
            with ppkgs; [
              numpy
              pandas
              h5py
              panna-python
              fast-hdbscan
              icecream
              sigmod-hdbscan.packages.${system}.default
              scikit-learn
              scipy
              matplotlib
              certifi
              filelock
            ]);

        container = pkgs.singularity-tools.buildImage {
          name = "panna";
          runScript = "#!${pkgs.stdenv.shell}\npython $@";
          contents = [
            python-interpreter
            pkgs.cacert
          ];
          diskSize = 1024 * 3; # necessary to fit the packages, otherwise the build fails
        };
      in {
        packages.default = panna-python;
        packages.container = container;
        packages.python = python-interpreter;

        devShells.default = (pkgs.mkShell.override {stdenv = pkgs.clangStdenv;}) {
          venvDir = ".venv";

          packages = with pkgs; [
            gcc
            lldb
            clang-tools
            python.pkgs.venvShellHook
            (
              python.withPackages
              (ps:
                with ps; [
                  build
                  numpy
                  pandas
                  matplotlib
                  seaborn
                  filelock
                  tornado
                  umap-learn
                  h5py
                  nanobind
                  icecream
                  scikit-build-core
                  certifi
                  sigmod-hdbscan.packages.${system}.default
                ])
            )
            hdf5
            sqlite-interactive
            cmake
            just
            bear # To generate compile_commands.json files
            llvmPackages.openmp
            llvmPackages.libcxx
            rr
            gdbgui
            valgrind
            highfive
            samply
            boost
            cereal
            catch2_3
            fast-hdbscan
            ensmallen
            mlpack
            armadillo
            hl-bin
            tree-similarity
          ];

          NIX_ENFORCE_NO_NATIVE = false;
        };
      }
    );
}
